import "../txUtil.scrypt";
import "stakeProto.scrypt";
import "../uniqueProto.scrypt";
import "../token/tokenProto.scrypt";
import "../MerkleTree.scrypt";
import "../blockHeight.scrypt";
import "stakeCommon.scrypt";

// opreturn: stakeTokenHolderHash + stakeCodeHash
contract StakeWithdraw {
    bytes stakeID;
    bytes tokenID;
    bytes tokenScriptCodeHash;
    Ripemd160 adminAddress;

    // max input num 6 * 36
    static const int MAX_PREVOUTS_LEN = 216;

    static const int stakeInputIndex = 1;
    static const int poolTokenInputIndex = 2;

    public function unlock(SigHashPreimage txPreimage,
        bytes prevouts,
        // sig
        PubKey senderPubKey,
        Sig senderSig,
        int tokenRemoveAmount,
        // stake tx proof
        bytes stakeScript,
        TxOutputProof stakeTxProof,
        // pool token
        bytes poolTokenScript,
        TxOutputProof poolTokenTxProof,
        // block height
        BlockRabinData blockRabinData,
        // merkle
        bytes oldLeaf,
        bytes merklePath,
        // output
        int stakeOutputSatoshis,
        int poolTokenOutputSatoshis,
        Ripemd160 changeAddress,
        int changeSatoshis,
        bytes opReturnScript) {

        // check prevouts
        require(hash256(prevouts) == SigHash.hashPrevouts(txPreimage));
        require(len(prevouts) <= MAX_PREVOUTS_LEN);

        // getDataPart
        bytes lockingScript = SigHash.scriptCode(txPreimage);
        StakeContractData contractData = StakeProto.getOpContractData(lockingScript);

        // verify stake input
        bytes stakeOutpoint = prevouts[stakeInputIndex * TxUtil.OUTPOINT_LEN : (stakeInputIndex + 1) * TxUtil.OUTPOINT_LEN];
        TxOutputProof stakeProof = { stakeTxProof.txHeader, stakeTxProof.hashProof, stakeTxProof.satoshiBytes, sha256(stakeScript) };
        TxUtil.verifyTxOutput(stakeProof, stakeOutpoint);

        int stakeScriptLen = len(stakeScript);
        require(this.stakeID == UniqueProto.getUniqueID(stakeScript, stakeScriptLen));

        StakeInfo stakeInfo = StakeCommon.checkStakeInput(stakeScript, this.stakeID, contractData.stakeContractCodeHash);

        // can withdraw when withdrawLockInterval == 0
        require(stakeInfo.withdrawLockInterval == 0);

        // verify pool token input
        int poolTokenScriptLen = len(poolTokenScript);
        bytes poolTokenOutpoint = prevouts[poolTokenInputIndex * TxUtil.OUTPOINT_LEN : (poolTokenInputIndex + 1) * TxUtil.OUTPOINT_LEN];
        TxOutputProof poolTokenProof = { poolTokenTxProof.txHeader, poolTokenTxProof.hashProof, poolTokenTxProof.satoshiBytes, sha256(poolTokenScript) };
        TxUtil.verifyTxOutput(poolTokenProof, poolTokenOutpoint);
        require(this.tokenID == TokenProto.getTokenID(poolTokenScript, poolTokenScriptLen));
        require(this.tokenScriptCodeHash == TokenProto.getScriptCodeHash(poolTokenScript, poolTokenScriptLen));

        // get blockNum from rabinMsg
        int curBlockTime = StakeCommon.verifyBlockRabinMsg(stakeScript, prevouts, stakeInputIndex, blockRabinData, stakeInfo.lastRewardTime);

        // verify sig
        require(checkSig(senderSig, senderPubKey));
        bytes senderAddress = hash160(senderPubKey);

        LeafData data = StakeCommon.getLeafData(oldLeaf);
        // admin could withdraw token to user address
        if (data.address != senderAddress) {
            require(senderAddress == this.adminAddress);
            senderAddress = data.address;
        }
        //require(data.address == senderAddress);
        require(data.tokenAmount >= tokenRemoveAmount);

        // updatePool
        int newAccPoolPerShare = StakeCommon.updatePool(stakeInfo, curBlockTime);
        int newPoolTokenAmount = stakeInfo.poolTokenAmount - tokenRemoveAmount;
        data.tokenAmount -= tokenRemoveAmount;
        data.rewardDebt -= tokenRemoveAmount * newAccPoolPerShare / stakeInfo.rewardAmountFactor;

        bytes newLeaf = StakeCommon.genNewLeaf(data);

        // update user data
        bytes newUserDataMerkleRoot = MerkleTree.updateLeaf(sha256(oldLeaf), sha256(newLeaf), merklePath, stakeInfo.userDataMerkleRoot);

        // stake output
        bytes newStakeScript = StakeProto.getNewStakeScript(stakeScript, curBlockTime, newPoolTokenAmount, 0, newAccPoolPerShare, newUserDataMerkleRoot);
        bytes stakeOutput = Utils.buildOutput(newStakeScript, stakeOutputSatoshis);

        // user token output
        bytes tokenScript = TokenProto.getNewTokenScript(poolTokenScript, poolTokenScriptLen, senderAddress, tokenRemoveAmount);
        bytes tokenOutput = Utils.buildOutput(tokenScript, poolTokenOutputSatoshis);

        // pool token output
        bytes poolTokenOutput = b'';
        if (newPoolTokenAmount > 0) {
            bytes newPoolScript = TokenProto.getNewTokenScript(poolTokenScript, poolTokenScriptLen, contractData.stakeTokenHolderHash, newPoolTokenAmount);
            poolTokenOutput = Utils.buildOutput(newPoolScript, poolTokenOutputSatoshis);
        }

        bytes changeOutput = TxUtil.genMvcOutput(changeSatoshis, changeAddress);

        // op_false op_return output
        bytes opReturnOutput = b'';
        if (len(opReturnScript) > 0) {
            require(opReturnScript[0 : 2] == b'006a');
            opReturnOutput = Utils.buildOutput(opReturnScript, 0);
        }

        bytes outputs = stakeOutput + tokenOutput + poolTokenOutput + opReturnOutput + changeOutput;

        require(hash256(outputs) == SigHash.hashOutputs(txPreimage));
        require(Tx.checkPreimageOCS(txPreimage));
    }
}
